module names

language mod

//
// Direct references
//

test definition reference - direct, public [[ 
  pub def [[d]] = 1
  pub def e = [[d]] 
]]
0 errors
0 warnings
resolve #2 to #1

test definition reference - direct, private [[ 
  prv def [[d]] = 1
  pub def e = [[d]] 
]]
0 errors
0 warnings
resolve #2 to #1

test definition reference - self [[
  pub def [[d]] : int = [[d]]
]]
0 errors
0 warnings
resolve #2 to #1

test unresolved definition reference - direct [[ 
  pub def d = [[e]] 
]]
2 errors at #1
0 warnings



//
// Lexically scoped references
//

test definition reference - lexical scope, public [[
  pub def [[d]] = 1
  pub mod m {
    pub def e = [[d]]
  }
]]
0 errors
0 warnings
resolve #2 to #1

/*
Private definitions are visible to children scopes.
*/
test definition reference - lexical scope, private [[
  prv def [[d]] = 1
  pub mod m {
    pub def e = [[d]]
  }
]]
0 errors
0 warnings
resolve #2 to #1

test unresolved definition reference - lexical scope, forward [[
  pub def d = 1
  pub mod m {
    pub def e = [[f]]
  }
]]
2 errors at #1
0 warnings

/*
Definitions are not directly visible to parent scopes.
*/
test unresolved definition reference - lexical scope, backwards [[
  pub def d = [[e]]
  pub mod m {
    pub def e = 1
  }
]]
2 errors at #1
0 warnings



//
// Qualified references
//

test references - two-level qualified [[
  pub def d = [[m]].[[e]]
  pub mod [[m]] {
    pub def [[e]] = 1
  }
]]
0 errors
0 warnings
resolve #1 to #3
resolve #2 to #4 
   
test unresolved definition reference - two-level qualified [[
  pub def d = [[m]].f
  pub mod [[m]] {
    pub def e = 1
  }
]]
2 errors
0 warnings
resolve #1 to #2

test unresolved module reference - two-level qualified [[
  pub def d = [[n.e]]
  pub mod m {
    pub def e = 1
  }
]]
3 errors at #1

/*
Private definitions are not visible to parent scopes.
*/
test inaccessible definition reference - two-level qualified [[
  pub def d = [[m]].e
  pub mod [[m]] {
    prv def e = 1
  }
]]
2 errors
0 warnings
resolve #1 to #2

/*
Private modules and their elements are not visible to parent scopes.
*/
test inaccessible module reference - two-level qualified [[
  pub def d = [[m.e]]
  prv mod m {
    pub def e = 1
  }
]]
3 errors at #1

test references - three-level qualified [[
  pub def d = [[m]].[[n]].[[e]]
  pub mod [[m]] {
    pub mod [[n]] {
      pub def [[e]] = 1
    }
  }
]]
0 errors
0 warnings
resolve #1 to #4
resolve #2 to #5
resolve #3 to #6

test references - four-level qualified [[
  pub def d = [[m]].[[n]].[[o]].[[e]]
  pub mod [[m]] {
    pub mod [[n]] {
      pub mod [[o]] {
        pub def [[e]] = 1
      }
    }
  }
]]
0 errors
0 warnings
resolve #1 to #5
resolve #2 to #6
resolve #3 to #7
resolve #4 to #8



//
// Single-level (non-transitive) imports
//

test references - one-level import [[
  prv imp [[m]].*
  pub def d = [[e]]
  pub mod [[m]] {
    pub def [[e]] = 1
  }
]]
0 errors
0 warnings
resolve #1 to #3
resolve #2 to #4

test references - one-level import, multiple imports [[
  prv imp [[m]].*
  pub def d = [[e]] + [[f]]
  pub mod [[m]] {
    pub def [[e]] = 1
    pub def [[f]] = 2
  }
]]
0 errors
0 warnings
resolve #1 to #4
resolve #2 to #5
resolve #3 to #6

/*
Private definitions are not visible to parent scopes, even when 
imported.
*/
test inaccessible definition reference - one-level import [[
  prv imp [[m]].*
  pub def d = [[e]]
  pub mod [[m]] {
    prv def e = 1
  }
]]
2 errors at #2
0 warnings
resolve #1 to #3

/*
Private modules are not visible to parent scopes.
*/
test inaccessible module reference - one-level import [[
  prv imp [[m.*]]
  pub def d = e
  prv mod m {
    pub def e = 1
  }
]]
3 errors at #1
0 warnings

test references - one-level import, qualified [[
  prv imp [[m]].[[n]].[[o]].*
  pub def d = [[e]]
  pub mod [[m]] {
    pub mod [[n]] { 
      pub mod [[o]] {
        pub def [[e]] = 1
      }
    }
  }
]]
0 errors
0 warnings
resolve #1 to #5
resolve #2 to #6
resolve #3 to #7
resolve #4 to #8



//
// Multi-level (transitive) imports
//

test references - two-level import [[
  pub mod main {
    prv imp [[m]].*
    pub def d = [[e]] + [[f]]
  }
  pub mod [[m]] {
    pub imp [[n]].*
    pub def [[e]] = 1
  }
  pub mod [[n]] {
    pub def [[f]] = 2
  }
]]
0 errors
0 warnings
resolve #1 to #4
resolve #2 to #6
resolve #3 to #8
resolve #5 to #7

test unresolved module (import) reference - two-level import [[
  pub mod main {
    prv imp m.*
    pub def d = [[e]] + f
  }
  pub mod m {
    prv imp [[n]].*
    pub def [[e]] = 1
  }
  pub mod [[n]] {
    pub def f = 2
  }
]]
2 errors
0 warnings
resolve #1 to #3
resolve #2 to #4

test references - three-level import [[
  pub mod main {
    prv imp [[m]].*
    pub def d = [[e]] + [[f]] + [[g]]
  }
  pub mod [[m]] {
    pub imp [[n]].*
    pub def [[e]] = 1
  }
  pub mod [[n]] {
    pub imp [[o]].*
    pub def [[f]] = 2
  }
  pub mod [[o]] {
    pub def [[g]] = 3
  }
]]
0 errors
0 warnings
resolve #1 to #5
resolve #2 to #7
resolve #3 to #10
resolve #4 to #12
resolve #6 to #8
resolve #9 to #11

test references - staged imports [[
  pub mod main {
    prv imp [[m]].*
    prv imp [[n]].*
    prv imp [[o]].*
    pub def d = [[e]] + [[f]] + [[g]]
  }
  pub mod [[m]] {
    pub def [[e]] = 1
  }
  pub mod [[n]] {
    pub def [[f]] = 2
  }
  pub mod [[o]] {
    pub def [[g]] = 3
  }
]]
0 errors
0 warnings
resolve #1 to #7
resolve #2 to #9
resolve #3 to #11
resolve #4 to #8
resolve #5 to #10
resolve #6 to #12

test references - staged qualified imports [[
  pub mod main {
    prv imp [[m]].*
    prv imp [[n]].[[o]].*
    pub def d = [[e]] + [[g]]
  }
  pub mod [[m]] {
    pub def [[e]] = 1
  }
  pub mod [[n]] {
    pub def f = 2
    pub mod [[o]] {
      pub def [[g]] = 3
    }
  }
]]
0 errors
0 warnings
resolve #1 to #6
resolve #2 to #8
resolve #3 to #9
resolve #4 to #7
resolve #5 to #10